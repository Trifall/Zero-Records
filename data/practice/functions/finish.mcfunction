# the dragon is left to play out the full 10s ending death animation. the finish
# is confirmed the tick that animation starts, so its constant 200t tail is added
# up front instead of waited out - its parts still answer the selector then. the
# fallback (dragon gone outright) already lived through the tail.
execute if entity @e[type=minecraft:ender_dragon,limit=1] run scoreboard players add timer timer 200
scoreboard players set active timer 0
# keeps prediction/lock out of the post-confirm window; cleared when the next
# kill saves its record
scoreboard players set #finish_locked zc_ctrl 1
execute unless score onecycle flags matches 1 run function zeroboard:records/finish

function practice:timer/timer

# flying_to_fountain deliberately stays set through the death animation so
# health_display keeps fountain-flight mode (bossbar empty, pool untouched);
# practice:reset clears it with the rest of the attempt

execute if score timer settings matches 0 unless score onecycle flags matches 1 run tellraw @a [{"text":"\nFinal Time: "},{"nbt":"time_string","storage":"practice:timeparser","interpret":true,"color":"gold"}]
